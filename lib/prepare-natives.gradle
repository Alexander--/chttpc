buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath 'de.undercouch:gradle-download-task:1.2'
    }
}

import de.undercouch.gradle.tasks.download.*
import java.util.regex.Pattern

apply plugin: DownloadTaskPlugin

def curlNdkTaskPattern = Pattern.compile("ndkBuildCurlAbi_(.+)");

tasks.addRule("NDK tasks") { String taskName ->
    def m = curlNdkTaskPattern.matcher(taskName);

    if (m.matches()) {
        def abi = m.group(1)

        def toolchainDir = "$projectDir.absolutePath/external/build/toolchain"

        def gccVer = "4.9"

        def toolchain
        def hostName
        def arch

        switch (abi) {
            case 'x86':
                hostName = 'i686-linux-android'
                toolchain = "x86-${gccVer}"
                arch = 'arch-x86'
                break
            case 'mips':
                hostName = 'mipsel-linux-android'
                toolchain = "mipsel-linux-android-${gccVer}"
                arch = 'arch-mips'
                break
            case 'armeabi_v7a':
                abi = 'armeabi-v7a'
                hostName = 'arm-linux-androideabi'
                toolchain = "arm-linux-androideabi-${gccVer}"
                arch = 'arch-arm'
                break
            default:
                throw new UnsupportedOperationException(abi + " not supported!")
        }

        def prepTask = taskName.replace("BuildCurl", "Prepare")
        prepTask = task(type: Exec, prepTask) {
            def ndkDir = ndkProps['ndk.dir']

            commandLine "$ndkDir/build/tools/make-standalone-toolchain.sh", "--platform=android-15",
                    "--toolchain=$toolchain", "--install-dir=$toolchainDir/$abi", "--stl=libc++", "--force"

            inputs.file "$ndkDir/build/tools/make-standalone-toolchain.sh"

            outputs.dir "$toolchainDir/$abi"
        }

        task(type: Exec, taskName) {
            environment 'ANDROID_NDK', ndkProps['ndk.dir']

            commandLine "$projectDir.absolutePath/external/build-natives.sh", "$hostName", "$abi", "$toolchain", "$arch"

            inputs.files "$projectDir.absolutePath/external/build-natives.sh",
                         "$projectDir.absolutePath/external/build-openssl.sh",
                         "$projectDir.absolutePath/external/build-c-ares.sh",
                         "$projectDir.absolutePath/external/setenv-generic.sh",
                         "$projectDir.absolutePath/external/setenv-simple.sh"

            outputs.dir "$projectDir/build/native-libs/$abi"

            workingDir "$projectDir.absolutePath/external"

            dependsOn prepTask
        }
    }
}

/*
import de.undercouch.gradle.tasks.download.Download

task downloadArmv5binary(type: Download) {
    src "http://sourceforge.net/projects/aria2/files/stable/aria2-1.18.8/aria2-1.18.8-android-build1.zip"
    dest new File(buildDir, 'armv5aria.zip')
    onlyIfNewer true
}

task downloadAndUnzipArmv5Binary(dependsOn: downloadArmv5binary, type: Copy) {
    from zipTree(downloadArmv5binary.dest)

    eachFile { FileCopyDetails fcp ->
        logger.log LogLevel.WARN, fcp.toString()

        if (fcp.relativePath.pathString.endsWith('aria2c')) {
            def segments = fcp.relativePath.segments
            def pathsegments =segments[1..-1] as String[]
            fcp.relativePath = new RelativePath(!fcp.file.isDirectory(), pathsegments)
        }
        else
            fcp.exclude()
    }

    includeEmptyDirs = false

    rename('aria2c', 'aria2')

    into "$projectDir.absolutePath/src/main/jniLibs/armeabi"
}
*/
task renameExecutables(type: Copy) {
    from "$projectDir.absolutePath/src/main/jniLibs"
    into "$projectDir.absolutePath/src/main/jniLibs"
    include '**/*'
    exclude '**/*.so'
    exclude '**/*.jar'
    rename(/(.+)/, 'lib$1.a')
}

tasks.renameExecutables.dependsOn ndkBuildCurlAbi_mips, ndkBuildCurlAbi_x86, ndkBuildCurlAbi_armeabi_v7a

project.android.libraryVariants.all { variant ->
    variant.externalNativeBuildTasks[0].dependsOn renameExecutables
}
