apply plugin: 'com.android.library'

apply from: "$projectDir/prepare-natives.gradle"

final int nativeVersionCode = 0

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.2'

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 1
        versionName '0.1'

        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
        testInstrumentationRunnerArguments [['disableAnalytics' : 'true']]

        buildConfigField 'int', 'NATIVE_VER', "$nativeVersionCode"

        externalNativeBuild {
            cmake {
                abiFilters 'x86', 'armeabi-v7a', 'mips'
                arguments "-DCHTTPC_NATIVE_VER:STRING=$nativeVersionCode", "-DANDROID_PLATFORM=android-15", "-DANDROID_TOOLCHAIN=gcc", "-DANDROID_ARM_MODE=thumb", "-DCMAKE_VERBOSE_MAKEFILE:BOOL=ON"
                cFlags '-std=gnu11'
            }
        }

        ndk {
            abiFilters 'x86', 'armeabi-v7a', 'mips'
        }

    }
    buildTypes {
        debug {
            testCoverageEnabled = true
        }
        release {
            minifyEnabled true
            proguardFiles 'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        cmake {
            path 'src/main/jni/CMakeLists.txt'
        }
    }
}

task jacocoTestReport(type: JacocoReport, dependsOn: 'createDebugCoverageReport') {
    group = "Reporting"
    description = "Generate Jacoco coverage reports"

    reports {
        xml.enabled = true
        html.enabled = true
    }

    executionData = files('build/jacoco/testDebugUnitTest.exec')

    sourceDirectories = files('src/main/java')

    classDirectories = files('build/intermediates/classes/debug')
}

repositories {
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
}

dependencies {
    compile 'net.sf.xfd:i10n:0.3.1'

    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    androidTestCompile 'com.squareup.okhttp3:mockwebserver:3.9.0-SNAPSHOT'
    androidTestCompile 'com.squareup.okhttp3:okhttp:3.9.0-SNAPSHOT'

    //compile 'com.carrotsearch:hppc:0.7.2'
    //provided 'com.koloboke:koloboke-compile:0.5.1'
    //compile 'com.koloboke:koloboke-impl-common-jdk6-7:1.0.0'

    compile 'com.android.support:support-annotations:25.1.0'

    testCompile 'junit:junit:4.12'
}
